
VAR wine_cnt = 0
VAR drink = false
VAR if_talked = false
-> waiter

 === function raise(ref x)
 	~ x = x + 1


===waiter===
-{not if_talked :您好！欢迎您光临白夜酒吧，请问，您有什么需要服务的么？|还有什么需要服务的吗？}#w
~if_talked = true
* 我想和你聊一聊。#Mayer
->consult
*[喝酒]#Mayer
->wine
* ...#Mayer
-> DONE

=wine
-请问，您需要什么样的酒呢？#Mayer

*  来一扎啤酒！#Mayer
     给您满上,满满一大杯啤酒，清爽怡人，能帮您忘了一切苦恼呢！#w
    ~raise(wine_cnt)
    -> wine
*  来一杯SUGER RUSH。#Mayer
     想不到，您这样的人物，居然会选择如此温柔的酒呢。啊，抱歉，我有些失态啦......#w
    ~raise(wine_cnt)
    -> wine
*  来一杯VELVET UNDERGROUD。#Mayer
     啊，前卫的艺术家们，都很喜欢这样具有美感气息的酒呢！#w
    ~raise(wine_cnt)
    -> wine

*  我不想喝了......#Mayer
{wine_cnt==3:->drunk|-> waiter}


=drunk
-呃......头要晕了，居然喝了这么多吗？要清醒一些的好，Mayer，Masha还在等你......#Mayer
-> waiter
->DONE
VAR if_consulted = false
=consult
-{if_consulted:您还想一些知道什么？|您想要知道什么周围的小道消息？关于酒吧，还是吉布森，您都可以同我畅聊——但是，出于职业道德，我是不能透露其他客人信息的，望您谅解。}
~if_consulted = true
*(if_android)[酒吧为什么在地下？]
    这里是白夜酒吧，一家新开的店。你想知道这个地下坑道以前是什么样吗？这块地方可有意思啦，这可是上次仿生人战争里，那些电子机械总脑的基地！那些大脑会短路的家伙没有人类所拥有的任何情感。#w
    剩下的，只有可怜的逻辑计算——这个基地，最后被GB Amry用EMP电子冲击波一锅端了，你说好不好笑？按我说的话，机械脑子，注定是比不上人脑的啊——#w
    -> consult
*[关于吉布森，我想了解一下这颗星球。]
    我看您也不像是第一次来吉布森啊，怎么聊起天来就像电子游戏里面一样的问着各种问题呢？哈哈哈哈，我开玩笑呢，电子游戏世界什么的，怎么可能呢——咳咳，实在是抱歉，我跑题了。吉布森区啊——对于我们这样的人来说，这里简直就是天堂！#w
    这儿的黑市能找到你想要的一切，生物器官，违禁义肢，军事产品，还有超级超级给劲的特殊LSD迷幻剂！这些东西，可都是那些号称文明的星球怎么找，都不可能找得到的违禁品呢！#w
    ->consult
*{if_android}[仿生人战争？]
    您难道真的是第一次来这里？貌似是几十年前，那群机械脑子的人又不安分了，按官方的说法，好像叫什么——第五次仿生人战争？但按照民间的说法，这次战争似乎和全球人们的地热发动机有关。#w
    虽然这些仿生人在几个月后就被剿灭了，但带头的那位机器疯子，带着一小支精英部队，突破重重阻碍，他们最后甚至都打到了Avatar公司大楼门前了呢。要不是Avatar BIO的人体强化技术的突破，说不定他们还能再多撑几个月，哈哈哈哈......嗯，话说回来，您是怎么看待那群机械脑子的呢？#w
    ** 仿生人......注定不会是人的。#Mayer
    ->consult
    ** 相信我，仿生人战争，远远没有结束。#Mayer
    ->consult
*[来这里的客人]
    实在是抱歉呢，我已经说过了，我不能透露任何关于其他客人的信息。白夜酒吧可不是普通的寻欢作乐的地点，每一个来白夜酒吧的人，都有自己的秘密，相信拿着武器，深度改造过后的您，也是这样的吧。#w
    ->consult
* 我没什么好问的了。#Mayer
 谢谢惠顾，白夜酒吧，祝您享受愉快！#w
->DONE
    谢谢惠顾，白夜酒吧，祝您享受愉快！#w